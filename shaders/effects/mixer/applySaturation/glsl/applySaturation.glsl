#version 300 es
precision highp float;

uniform sampler2D tex0;
uniform sampler2D tex1;
uniform vec2 resolution;
uniform float mixAmt;
out vec4 fragColor;

float map(float value, float inMin, float inMax, float outMin, float outMax) {
    return outMin + (outMax - outMin) * (value - inMin) / (inMax - inMin);
}

vec3 rgb2hsv(vec3 c) {
    vec4 K = vec4(0.0, -1.0/3.0, 2.0/3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c) {
    vec4 K = vec4(1.0, 2.0/3.0, 1.0/3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main() {
    vec2 st = gl_FragCoord.xy / resolution;
    

    vec4 color1 = texture(tex0, st);
    vec4 color2 = texture(tex1, st);

    // saturation from B: hue/value from A, saturation from B
    vec3 a = rgb2hsv(color1.rgb);
    vec3 b = rgb2hsv(color2.rgb);
    vec3 middle = hsv2rgb(vec3(a.x, b.y, a.z));

    float amt = map(mixAmt, -100.0, 100.0, 0.0, 1.0);
    vec4 color;
    if (amt < 0.5) {
        color = mix(color1, vec4(middle, color1.a), amt * 2.0);
    } else {
        color = mix(vec4(middle, color1.a), color2, (amt - 0.5) * 2.0);
    }

    color.a = max(color1.a, color2.a);
    fragColor = color;
}
