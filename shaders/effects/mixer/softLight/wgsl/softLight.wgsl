@group(0) @binding(0) var samp : sampler;
@group(0) @binding(1) var tex0 : texture_2d<f32>;
@group(0) @binding(2) var tex1 : texture_2d<f32>;
@group(0) @binding(3) var<uniform> mixAmt : f32;

fn map_range(value : f32, inMin : f32, inMax : f32, outMin : f32, outMax : f32) -> f32 {
    return outMin + (outMax - outMin) * (value - inMin) / (inMax - inMin);
}

fn blendSoftLight(base : f32, blend : f32) -> f32 {
    if (blend < 0.5) {
        return 2.0 * base * blend + base * base * (1.0 - 2.0 * blend);
    }
    return sqrt(base) * (2.0 * blend - 1.0) + 2.0 * base * (1.0 - blend);
}

@fragment
fn main(@builtin(position) position : vec4<f32>) -> @location(0) vec4<f32> {
    let dims = vec2<f32>(textureDimensions(tex0, 0));
    var st = position.xy / dims;
    

    let color1 = textureSample(tex0, samp, st);
    let color2 = textureSample(tex1, samp, st);

    // soft light blend
    let middle = vec4<f32>(
        blendSoftLight(color1.r, color2.r),
        blendSoftLight(color1.g, color2.g),
        blendSoftLight(color1.b, color2.b),
        mix(color1.a, color2.a, 0.5)
    );

    let amt = map_range(mixAmt, -100.0, 100.0, 0.0, 1.0);
    var color : vec4<f32>;
    if (amt < 0.5) {
        color = mix(color1, middle, amt * 2.0);
    } else {
        color = mix(middle, color2, (amt - 0.5) * 2.0);
    }

    color.a = max(color1.a, color2.a);
    return color;
}
