#version 300 es

/*
 * Atmosphere shader.
 * Generates layered volumetric clouds and gradients with deterministic noise to support preset recall.
 * Sun position and wind parameters are remapped to normalized vectors to maintain stability.
 */

precision highp float;
precision highp int;

uniform sampler2D noiseTex;
uniform float time;
uniform float seed;
uniform bool wrap;
uniform vec2 resolution;
uniform int noiseType;
uniform float noiseScale;
uniform int colorMode;
uniform int interp;
uniform float refractAmt;
uniform float loopAmp;
uniform float hueRotation;
uniform float hueRange;
uniform float intensity;
uniform bool ridges;
uniform vec4 color1;
uniform vec4 color2;
uniform vec4 color3;
uniform vec4 color4;
out vec4 fragColor;

#define PI 3.14159265359
#define TAU 6.28318530718
#define aspectRatio resolution.x / resolution.y

float map(float value, float inMin, float inMax, float outMin, float outMax) {
    return outMin + (outMax - outMin) * (value - inMin) / (inMax - inMin);
}

// PCG PRNG - MIT License
// https://github.com/riccardoscalco/glsl-pcg-prng
uvec3 pcg(uvec3 v) {
	v = v * uint(1664525) + uint(1013904223);

	v.x += v.y * v.z;
	v.y += v.z * v.x;
	v.z += v.x * v.y;

	v ^= v >> uint(16);

	v.x += v.y * v.z;
	v.y += v.z * v.x;
	v.z += v.x * v.y;

	return v;
}

vec3 prng (vec3 p) {
    p.x = p.x >= 0.0 ? p.x * 2.0 : -p.x * 2.0 + 1.0;
    p.y = p.y >= 0.0 ? p.y * 2.0 : -p.y * 2.0 + 1.0;
    p.z = p.z >= 0.0 ? p.z * 2.0 : -p.z * 2.0 + 1.0;
    return vec3(pcg(uvec3(p))) / float(uint(0xffffffff));
}
// end PCG PRNG

vec3 brightnessContrast(vec3 color) {
    float bright = map(intensity, -100.0, 100.0, -0.4, 0.4);
    float cont = 1.0;
    if ( intensity < 0.0) {
        cont = map(intensity, -100.0, 0.0, 0.5, 1.0);
    } else {
        cont = map(intensity, 0.0, 100.0, 1.0, 1.5);
    }

    color = (color - 0.5) * cont + 0.5 + bright;
    return color;
}

vec3 hsv2rgb(vec3 hsv) {
    float h = fract(hsv.x);
    float s = hsv.y;
    float v = hsv.z;
    
    float c = v * s; // Chroma
    float x = c * (1.0 - abs(mod(h * 6.0, 2.0) - 1.0));
    float m = v - c;

    vec3 rgb;

    if (0.0 <= h && h < 1.0/6.0) {
        rgb = vec3(c, x, 0.0);
    } else if (1.0/6.0 <= h && h < 2.0/6.0) {
        rgb = vec3(x, c, 0.0);
    } else if (2.0/6.0 <= h && h < 3.0/6.0) {
        rgb = vec3(0.0, c, x);
    } else if (3.0/6.0 <= h && h < 4.0/6.0) {
        rgb = vec3(0.0, x, c);
    } else if (4.0/6.0 <= h && h < 5.0/6.0) {
        rgb = vec3(x, 0.0, c);
    } else if (5.0/6.0 <= h && h < 1.0) {
        rgb = vec3(c, 0.0, x);
    } else {
        rgb = vec3(0.0, 0.0, 0.0);
    }

    return rgb + vec3(m, m, m);
}

vec3 rgb2hsv(vec3 rgb) {
    float r = rgb.r;
    float g = rgb.g;
    float b = rgb.b;
    
    float max = max(r, max(g, b));
    float min = min(r, min(g, b));
    float delta = max - min;

    float h = 0.0;
    if (delta != 0.0) {
        if (max == r) {
            h = mod((g - b) / delta, 6.0) / 6.0;
        } else if (max == g) {
            h = ((b - r) / delta + 2.0) / 6.0;
        } else if (max == b) {
            h = ((r - g) / delta + 4.0) / 6.0;
        }
    }
    
    float s = (max == 0.0) ? 0.0 : delta / max;
    float v = max;

    return vec3(h, s, v);
}

vec3 linearToSrgb(vec3 linear) {
    vec3 srgb;
    for (int i = 0; i < 3; ++i) {
        if (linear[i] <= 0.0031308) {
            srgb[i] = linear[i] * 12.92;
        } else {
            srgb[i] = 1.055 * pow(linear[i], 1.0 / 2.4) - 0.055;
        }
    }
    return srgb;
}

// oklab transform and inverse - Public Domain/MIT License
// https://bottosson.github.io/posts/oklab/

const mat3 fwdA = mat3(1.0, 1.0, 1.0,
                       0.3963377774, -0.1055613458, -0.0894841775,
                       0.2158037573, -0.0638541728, -1.2914855480);

const mat3 fwdB = mat3(4.0767245293, -1.2681437731, -0.0041119885,
                       -3.3072168827, 2.6093323231, -0.7034763098,
                       0.2307590544, -0.3411344290,  1.7068625689);

const mat3 invB = mat3(0.4121656120, 0.2118591070, 0.0883097947,
                       0.5362752080, 0.6807189584, 0.2818474174,
                       0.0514575653, 0.1074065790, 0.6302613616);

const mat3 invA = mat3(0.2104542553, 1.9779984951, 0.0259040371,
                       0.7936177850, -2.4285922050, 0.7827717662,
                       -0.0040720468, 0.4505937099, -0.8086757660);

vec3 oklab_from_linear_srgb(vec3 c) {
    vec3 lms = invB * c;

    return invA * (sign(lms)*pow(abs(lms), vec3(0.3333333333333)));
}

vec3 linear_srgb_from_oklab(vec3 c) {
    vec3 lms = fwdA * c;

    return fwdB * (lms * lms * lms);
}
// end oklab

// periodic function for looping
float periodicFunction(float p) {
    return map(sin(p * TAU), -1.0, 1.0, 0.0, 1.0);
}

// Simplex 2D - MIT License
// https://github.com/ashima/webgl-noise/blob/master/src/noise2D.glsl
//
// Description : Array and textureless GLSL 2D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//               https://github.com/stegu/webgl-noise
// 
// Copyright (C) 2011 by Ashima Arts (Simplex noise)
// Copyright (C) 2011-2016 by Stefan Gustavson (Classic noise and others)
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
vec3 mod289(vec3 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
    return mod289(((x*34.0)+1.0)*x);
}

float simplexValue(vec2 st, float xFreq, float yFreq, float s, float blend) {
    const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                        0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                       -0.577350269189626,  // -1.0 + 2.0 * C.x
                        0.024390243902439); // 1.0 / 41.0

    vec2 uv = vec2(st.x * xFreq, st.y * yFreq);
    uv.x += s;

    // First corner
    vec2 i  = floor(uv + dot(uv, C.yy) );
    vec2 x0 = uv -   i + dot(i, C.xx);

    // Other corners
    vec2 i1;
    //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
    //i1.y = 1.0 - i1.x;
    i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
    // x0 = x0 - 0.0 + 0.0 * C.xx ;
    // x1 = x0 - i1 + 1.0 * C.xx ;
    // x2 = x0 - 1.0 + 2.0 * C.xx ;
    vec4 x12 = x0.xyxy + C.xxzz;
    x12.xy -= i1;

    // Permutations
    i = mod289(i); // Avoid truncation effects in permutation
    vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		  + i.x + vec3(0.0, i1.x, 1.0 ));

    vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
    m = m*m ;
    m = m*m ;

    // Gradients: 41 points uniformly over a line, mapped onto a diamond.
    // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

    vec3 x = 2.0 * fract(p * C.www) - 1.0;
    vec3 h = abs(x) - 0.5;
    vec3 ox = floor(x + 0.5);
    vec3 a0 = x - ox;

    // Normalise gradients implicitly by scaling m
    // Approximation of: m *= inversesqrt( a0*a0 + h*h );
    m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

    // Compute final noise value at P
    vec3 g;
    g.x  = a0.x  * x0.x  + h.x  * x0.y;
    g.yz = a0.yz * x12.xz + h.yz * x12.yw;

    float v = 130.0 * dot(m, g);

    return periodicFunction(map(v, -1.0, 1.0, 0.0, 1.0) - blend);
}
// end simplex

float sineNoise(vec2 st, float xFreq, float yFreq, float s, float blend) {
    vec2 uv = vec2(st.x * xFreq, st.y * yFreq);
    uv.x += s;

    float a = blend;
    float b = blend;
    float c = 1.0 - blend;

    vec3 r1 = prng(vec3(s, 0.0, 0.0)) * 0.75 + 0.125;
    vec3 r2 = prng(vec3(s + 10.0, 0.0, 0.0)) * 0.75 + 0.125;
    float x = sin(r1.x * uv.y + sin(r1.y * uv.x + a) + sin(r1.z * uv.x + b) + c);
    float y = sin(r2.x * uv.x + sin(r2.y * uv.y + b) + sin(r2.z * uv.y + c) + a);

    return (x + y) * 0.5 + 0.5;
}

// Noisemaker value noise - MIT License
// https://github.com/noisedeck/noisemaker/blob/master/noisemaker/value.py
int positiveModulo(int value, int modulus) {
    if (modulus == 0) {
        return 0;
    }

    int r = value % modulus;
    return (r < 0) ? r + modulus : r;
}

vec3 randomFromLatticeWithOffset(vec2 st, float xFreq, float yFreq, float s, ivec2 offset) {
    vec2 lattice = vec2(st.x * xFreq, st.y * yFreq);
    vec2 baseFloor = floor(lattice);
    ivec2 base = ivec2(baseFloor) + offset;
    vec2 frac = lattice - baseFloor;

    int seedInt = int(floor(s));
    float seedFrac = fract(s);

    float xCombined = frac.x + seedFrac;
    int xi = base.x + seedInt + int(floor(xCombined));
    int yi = base.y;

    if (wrap) {
        int freqXInt = int(xFreq + 0.5);
        int freqYInt = int(yFreq + 0.5);

        if (freqXInt > 0) {
            xi = positiveModulo(xi, freqXInt);
        }
        if (freqYInt > 0) {
            yi = positiveModulo(yi, freqYInt);
        }
    }

    uint xBits = uint(xi);
    uint yBits = uint(yi);
    uint seedBits = floatBitsToUint(seed);
    uint fracBits = floatBitsToUint(seedFrac);

    uvec3 jitter = uvec3(
        (fracBits * 374761393u) ^ 0x9E3779B9u,
        (fracBits * 668265263u) ^ 0x7F4A7C15u,
        (fracBits * 2246822519u) ^ 0x94D049B4u
    );

    uvec3 state = uvec3(xBits, yBits, seedBits) ^ jitter;
    uvec3 prngState = pcg(state);
    float denom = float(0xffffffffu);
    return vec3(
        float(prngState.x) / denom,
        float(prngState.y) / denom,
        float(prngState.z) / denom
    );
}

float constant(vec2 st, float xFreq, float yFreq, float s) {
    vec3 rand = randomFromLatticeWithOffset(st, xFreq, yFreq, s, ivec2(0, 0));
    float scaledTime = periodicFunction(rand.x - time) * map(abs(loopAmp), 0.0, 100.0, 0.0, 0.25);
    return periodicFunction(rand.y - scaledTime);
}

// ---- 3×3 quadratic B-spline interpolation ----
// Replaces legacy bicubic 4×4 (16 taps) with 3×3 kernel (9 taps)
// Performance: ~1.8× faster in fBm chains
// Quality: Quadratic B-spline (degree 2) smoothing, C¹ continuous

// Quadratic B-spline basis functions for 3 samples
float quadratic3(float p0, float p1, float p2, float t) {
    float t2 = t * t;
    return p0 * 0.5 * (1.0 - t) * (1.0 - t) +
           p1 * 0.5 * (-2.0 * t2 + 2.0 * t + 1.0) +
           p2 * 0.5 * t2;
}

// Catmull-Rom 3-point interpolation (degree 3, C⁰ continuous)
// Interpolates through all 3 points
float catmullRom3(float p0, float p1, float p2, float t) {
    float t2 = t * t;
    float t3 = t2 * t;
    
    return p1 + 0.5 * t * (p2 - p0) + 
           0.5 * t2 * (2.0*p0 - 5.0*p1 + 4.0*p2 - p0) +
           0.5 * t3 * (-p0 + 3.0*p1 - 3.0*p2 + p0);
}

float quadratic3x3Value(vec2 st, float xFreq, float yFreq, float s) {
    vec2 lattice = vec2(st.x * xFreq, st.y * yFreq);
    vec2 f = fract(lattice);
    
    // Sample 3×3 grid (9 taps)
    // Row -1 (y-1)
    float v00 = constant(st + vec2(-1.0/xFreq, -1.0/yFreq), xFreq, yFreq, s);
    float v10 = constant(st + vec2(0.0, -1.0/yFreq), xFreq, yFreq, s);
    float v20 = constant(st + vec2(1.0/xFreq, -1.0/yFreq), xFreq, yFreq, s);
    
    // Row 0 (y)
    float v01 = constant(st + vec2(-1.0/xFreq, 0.0), xFreq, yFreq, s);
    float v11 = constant(st, xFreq, yFreq, s);
    float v21 = constant(st + vec2(1.0/xFreq, 0.0), xFreq, yFreq, s);
    
    // Row 1 (y+1)
    float v02 = constant(st + vec2(-1.0/xFreq, 1.0/yFreq), xFreq, yFreq, s);
    float v12 = constant(st + vec2(0.0, 1.0/yFreq), xFreq, yFreq, s);
    float v22 = constant(st + vec2(1.0/xFreq, 1.0/yFreq), xFreq, yFreq, s);
    
    // Quadratic interpolation along x for each row
    float y0 = quadratic3(v00, v10, v20, f.x);
    float y1 = quadratic3(v01, v11, v21, f.x);
    float y2 = quadratic3(v02, v12, v22, f.x);
    
    // Quadratic interpolation along y
    return quadratic3(y0, y1, y2, f.y);
}

float catmullRom3x3Value(vec2 st, float xFreq, float yFreq, float s) {
    vec2 lattice = vec2(st.x * xFreq, st.y * yFreq);
    vec2 f = fract(lattice);
    
    // Sample 3×3 grid (9 taps)
    // Row -1 (y-1)
    float v00 = constant(st + vec2(-1.0/xFreq, -1.0/yFreq), xFreq, yFreq, s);
    float v10 = constant(st + vec2(0.0, -1.0/yFreq), xFreq, yFreq, s);
    float v20 = constant(st + vec2(1.0/xFreq, -1.0/yFreq), xFreq, yFreq, s);
    
    // Row 0 (y)
    float v01 = constant(st + vec2(-1.0/xFreq, 0.0), xFreq, yFreq, s);
    float v11 = constant(st, xFreq, yFreq, s);
    float v21 = constant(st + vec2(1.0/xFreq, 0.0), xFreq, yFreq, s);
    
    // Row 1 (y+1)
    float v02 = constant(st + vec2(-1.0/xFreq, 1.0/yFreq), xFreq, yFreq, s);
    float v12 = constant(st + vec2(0.0, 1.0/yFreq), xFreq, yFreq, s);
    float v22 = constant(st + vec2(1.0/xFreq, 1.0/yFreq), xFreq, yFreq, s);
    
    // Catmull-Rom interpolation along x for each row
    float y0 = catmullRom3(v00, v10, v20, f.x);
    float y1 = catmullRom3(v01, v11, v21, f.x);
    float y2 = catmullRom3(v02, v12, v22, f.x);
    
    // Catmull-Rom interpolation along y
    return catmullRom3(y0, y1, y2, f.y);
}

// ---- End 3×3 interpolation ----

// cubic B-spline interpolation (degree 3, C² continuous)
float blendBicubic(float p0, float p1, float p2, float p3, float t) {
    // Cubic B-spline basis functions for uniform knots
    // Provides C² continuous smoothing
    float t2 = t * t;
    float t3 = t2 * t;
    
    float b0 = (1.0 - t) * (1.0 - t) * (1.0 - t) / 6.0;
    float b1 = (3.0 * t3 - 6.0 * t2 + 4.0) / 6.0;
    float b2 = (-3.0 * t3 + 3.0 * t2 + 3.0 * t + 1.0) / 6.0;
    float b3 = t3 / 6.0;
    
    return p0 * b0 + p1 * b1 + p2 * b2 + p3 * b3;
}

// Catmull-Rom 4-point interpolation (standard, tension=0.5)
// Interpolates through middle 2 points (p1, p2)
float catmullRom4(float p0, float p1, float p2, float p3, float t) {
    return p1 + 0.5 * t * (p2 - p0 + t * (2.0 * p0 - 5.0 * p1 + 4.0 * p2 - p3 + 
           t * (3.0 * (p1 - p2) + p3 - p0)));
}

float blendLinearOrCosine(float a, float b, float amount, int interp) {
    if (interp == 1) {
        return mix(a, b, amount);
    }

    return mix(a, b, smoothstep(0.0, 1.0, amount));
}

// texture-based bicubic for better performance
// from http://www.java-gaming.org/index.php?topic=35123.0
vec4 cubic(float v){
    vec4 n = vec4(1.0, 2.0, 3.0, 4.0) - v;
    vec4 s = n * n * n;
    float x = s.x;
    float y = s.y - 4.0 * s.x;
    float z = s.z - 4.0 * s.y + 6.0 * s.x;
    float w = 6.0 - x - y - z;
    return vec4(x, y, z, w) * (1.0/6.0);
}

float textureBicubic(vec2 texCoords, float xFreq, float yFreq, float _seed, float blend) {
    texCoords.x *= aspectRatio;
    texCoords.x *= xFreq * xFreq / resolution.x * 0.5;
    texCoords.y *= xFreq * yFreq / resolution.y * 0.5;
    texCoords -= vec2(aspectRatio * 0.5, 0.5);

    vec2 texSize = vec2(512.0);
    vec2 invTexSize = 1.0 / texSize;

    texCoords = texCoords * texSize - 0.5;
    // Map to avoid image crease
    texCoords.x = mod(texCoords.x + map(_seed, -255.0, 255.0, 1.0, 100.0), texSize.x);

    vec2 fxy = fract(texCoords);
    texCoords -= fxy;

    vec4 xcubic = cubic(fxy.x);
    vec4 ycubic = cubic(fxy.y);

    vec4 c = texCoords.xxyy + vec2(-0.5, 1.5).xyxy;

    vec4 s = vec4(xcubic.xz + xcubic.yw, ycubic.xz + ycubic.yw);
    vec4 offset = c + vec4 (xcubic.yw, ycubic.yw) / s;

    offset *= invTexSize.xxyy;

    float sample0 = texture(noiseTex, offset.xz).r;
    float sample1 = texture(noiseTex, offset.yz).r;
    float sample2 = texture(noiseTex, offset.xw).r;
    float sample3 = texture(noiseTex, offset.yw).r;

    float sx = s.x / (s.x + s.y);
    float sy = s.z / (s.z + s.w);

    float temp = mix(mix(sample3, sample2, sx), mix(sample1, sample0, sx), sy);
    return periodicFunction(temp * 4.0 - blend);
}
// end texture-based bicubic

float bicubicValue(vec2 st, float xFreq, float yFreq, float s) {
    // Neighbor Distance
    float ndX = 1.0 / xFreq;
    float ndY = 1.0 / yFreq;

    float u0 = st.x - ndX;
    float u1 = st.x;
    float u2 = st.x + ndX;
    float u3 = st.x + ndX + ndX;

    float v0 = st.y - ndY;
    float v1 = st.y;
    float v2 = st.y + ndY;
    float v3 = st.y + ndY + ndY;

    float x0y0 = constant(vec2(u0, v0), xFreq, yFreq, s);
    float x0y1 = constant(vec2(u0, v1), xFreq, yFreq, s);
    float x0y2 = constant(vec2(u0, v2), xFreq, yFreq, s);
    float x0y3 = constant(vec2(u0, v3), xFreq, yFreq, s);

    float x1y0 = constant(vec2(u1, v0), xFreq, yFreq, s);
    float x1y1 = constant(st, xFreq, yFreq, s);
    float x1y2 = constant(vec2(u1, v2), xFreq, yFreq, s);
    float x1y3 = constant(vec2(u1, v3), xFreq, yFreq, s);

    float x2y0 = constant(vec2(u2, v0), xFreq, yFreq, s);
    float x2y1 = constant(vec2(u2, v1), xFreq, yFreq, s);
    float x2y2 = constant(vec2(u2, v2), xFreq, yFreq, s);
    float x2y3 = constant(vec2(u2, v3), xFreq, yFreq, s);

    float x3y0 = constant(vec2(u3, v0), xFreq, yFreq, s);
    float x3y1 = constant(vec2(u3, v1), xFreq, yFreq, s);
    float x3y2 = constant(vec2(u3, v2), xFreq, yFreq, s);
    float x3y3 = constant(vec2(u3, v3), xFreq, yFreq, s);

    vec2 uv = vec2(st.x * xFreq, st.y * yFreq);

    float y0 = blendBicubic(x0y0, x1y0, x2y0, x3y0, fract(uv.x));
    float y1 = blendBicubic(x0y1, x1y1, x2y1, x3y1, fract(uv.x));
    float y2 = blendBicubic(x0y2, x1y2, x2y2, x3y2, fract(uv.x));
    float y3 = blendBicubic(x0y3, x1y3, x2y3, x3y3, fract(uv.x));

    return clamp(blendBicubic(y0, y1, y2, y3, fract(uv.y)), 0.0, 1.0);
}

float catmullRom4x4Value(vec2 st, float xFreq, float yFreq, float s) {
    // Neighbor Distance
    float ndX = 1.0 / xFreq;
    float ndY = 1.0 / yFreq;

    float u0 = st.x - ndX;
    float u1 = st.x;
    float u2 = st.x + ndX;
    float u3 = st.x + ndX + ndX;

    float v0 = st.y - ndY;
    float v1 = st.y;
    float v2 = st.y + ndY;
    float v3 = st.y + ndY + ndY;

    float x0y0 = constant(vec2(u0, v0), xFreq, yFreq, s);
    float x0y1 = constant(vec2(u0, v1), xFreq, yFreq, s);
    float x0y2 = constant(vec2(u0, v2), xFreq, yFreq, s);
    float x0y3 = constant(vec2(u0, v3), xFreq, yFreq, s);

    float x1y0 = constant(vec2(u1, v0), xFreq, yFreq, s);
    float x1y1 = constant(st, xFreq, yFreq, s);
    float x1y2 = constant(vec2(u1, v2), xFreq, yFreq, s);
    float x1y3 = constant(vec2(u1, v3), xFreq, yFreq, s);

    float x2y0 = constant(vec2(u2, v0), xFreq, yFreq, s);
    float x2y1 = constant(vec2(u2, v1), xFreq, yFreq, s);
    float x2y2 = constant(vec2(u2, v2), xFreq, yFreq, s);
    float x2y3 = constant(vec2(u2, v3), xFreq, yFreq, s);

    float x3y0 = constant(vec2(u3, v0), xFreq, yFreq, s);
    float x3y1 = constant(vec2(u3, v1), xFreq, yFreq, s);
    float x3y2 = constant(vec2(u3, v2), xFreq, yFreq, s);
    float x3y3 = constant(vec2(u3, v3), xFreq, yFreq, s);

    vec2 uv = vec2(st.x * xFreq, st.y * yFreq);

    float y0 = catmullRom4(x0y0, x1y0, x2y0, x3y0, fract(uv.x));
    float y1 = catmullRom4(x0y1, x1y1, x2y1, x3y1, fract(uv.x));
    float y2 = catmullRom4(x0y2, x1y2, x2y2, x3y2, fract(uv.x));
    float y3 = catmullRom4(x0y3, x1y3, x2y3, x3y3, fract(uv.x));

    return clamp(catmullRom4(y0, y1, y2, y3, fract(uv.y)), 0.0, 1.0);
}

float value(vec2 st, float xFreq, float yFreq, float s) {
    float scaledTime = 1.0;
    if (interp == 3) {
        // 3×3 quadratic B-spline (9 taps)
        return quadratic3x3Value(st, xFreq, yFreq, s);
    } else if (interp == 4) {
        // budget - texture bicubic
        scaledTime = textureBicubic(st, xFreq, yFreq, s + 50.0, time) * loopAmp * 0.0025;
        return textureBicubic(st, xFreq, yFreq, s, scaledTime);
    } else if (interp == 5) {
        // 4×4 cubic B-spline (16 taps)
        return bicubicValue(st, xFreq, yFreq, s);
    } else if (interp == 7) {
        // 3×3 Catmull-Rom (9 taps)
        return catmullRom3x3Value(st, xFreq, yFreq, s);
    } else if (interp == 8) {
        // 4×4 Catmull-Rom (16 taps)
        return catmullRom4x4Value(st, xFreq, yFreq, s);
    } else if (interp == 10) {
        scaledTime = simplexValue(st, xFreq, yFreq, s + 50.0, time) * loopAmp * 0.0025;
        return simplexValue(st, xFreq, yFreq, s, scaledTime);
    } else if (interp == 11) {
        scaledTime = sineNoise(st, xFreq, yFreq, s + 50.0, time) * loopAmp * 0.0025;
        return sineNoise(st, xFreq, yFreq, s, scaledTime);
    }

    float x1y1 = constant(st, xFreq, yFreq, s);

    if (interp == 0) {
        return x1y1;
    }

    // Neighbor Distance
    float ndX = 1.0 / xFreq;
    float ndY = 1.0 / yFreq;

    float x1y2 = constant(vec2(st.x, st.y + ndY), xFreq, yFreq, s);
    float x2y1 = constant(vec2(st.x + ndX, st.y), xFreq, yFreq, s);
    float x2y2 = constant(vec2(st.x + ndX, st.y + ndY), xFreq, yFreq, s);

    vec2 uv = vec2(st.x * xFreq, st.y * yFreq);

    float a = blendLinearOrCosine(x1y1, x2y1, fract(uv.x), interp);
    float b = blendLinearOrCosine(x1y2, x2y2, fract(uv.x), interp);

    return clamp(blendLinearOrCosine(a, b, fract(uv.y), interp), 0.0, 1.0);
}

vec3 noise(vec2 st, float s) {
    float freq = 1.0;
    if (interp != 10 && wrap) {
        freq = floor(map(noiseScale, 1.0, 100.0, 6.0, 2.0));
    } else {
        if (interp == 10) {
            freq = map(noiseScale, 1.0, 100.0, 1.0, 0.5);
        } else {
            freq = map(noiseScale, 1.0, 100.0, 6.0, 1.0);
        }
    }

    vec3 color = vec3(
        value(st, freq, freq, 0.0 + s),
        value(st, freq, freq, 10.0 + s),
        value(st, freq, freq, 20.0 + s));

    // hue
    color.r = color.r * hueRange * 0.01;
    color.r += 1.0 - (hueRotation / 360.0);

    // saturation
    color.g *= 0.333;

    // brightness - ridges
    color.b = 1.0 - abs(color.b * 2.0 - 1.0);

    color = hsv2rgb(color);

    return color;
}
// end value noise

void main() {
    vec4 color = vec4(0.0, 0.0, 1.0, 1.0);
    vec2 st = gl_FragCoord.xy / resolution.y;
    st -= vec2(aspectRatio * 0.5, 0.5);

    if (noiseType == 0) {
        // caustic
        vec3 leftColor = noise(st, seed);
        vec3 rightColor = noise(st, seed + 10.0);

        // from the "reflect" mode in coalesce.
        vec3 left = min(leftColor * rightColor / (1.0 - rightColor * leftColor), vec3(1.0));
        vec3 right = min(rightColor * leftColor / (1.0 - leftColor * rightColor), vec3(1.0));

        color.rgb = brightnessContrast(mix(left, right, 0.5));
    } else if (noiseType == 1) {
        // moodscape
        float xFreq = 1.0;
        float yFreq = 1.0;
        if (interp != 4 && interp != 10 && wrap) {
            xFreq = floor(map(noiseScale, 1.0, 100.0, 3.0, 2.0));
            yFreq = xFreq;
        } else {
            if (interp == 10) {
                xFreq = map(noiseScale, 1.0, 100.0, 1.0, 0.25);
                yFreq = xFreq * 1.5;
            } else {
                xFreq = map(noiseScale, 1.0, 100.0, 1.5, 1.0);
                yFreq = xFreq * 1.5;
            }
        }

        float s = floor(seed);

        // Refract values
        float xRef = value(st, xFreq, yFreq, +20.0 + s);
        float yRef = value(st, xFreq, yFreq, +10.0 + s);

        float ref = map(refractAmt, 0.0, 100.0, 0.0, 2.5);
        vec2 uv = vec2(st.x + xRef * ref, st.y + yRef * ref);

        if (colorMode == 0) {
            color.rgb = vec3(value(uv, xFreq, yFreq, s));
        } else {
            color = vec4(
                value(uv, xFreq, yFreq, s),
                value(uv, xFreq, yFreq, 10.0 + s),
                value(uv, xFreq, yFreq, 20.0 + s),
                1.0);
        }

        if (colorMode == 0) {
            // grayscale
            if (ridges) {
                color = 1.0 - abs(color * 2.0 - 1.0);
            }
        } else if (colorMode == 1) {
            // rgb
            if (ridges) {
                color = 1.0 - abs(color * 2.0 - 1.0);
            }
            color.rgb = rgb2hsv(color.rgb);
            color.r = color.r * hueRange * 0.01;
            color.r += 1.0 - (hueRotation / 360.0);
            color.rgb = hsv2rgb(color.rgb);
        } else if (colorMode == 2) {
            // hsv
            color.r = color.r * hueRange * 0.01;
            color.r += 1.0 - (hueRotation / 360.0);
            if (ridges) {
                color.b = 1.0 - abs(color.b * 2.0 - 1.0);
            }
            color.rgb = hsv2rgb(color.rgb);
        } else {
            // oklab
            // magic values from py-noisemaker - MIT License
            // https://github.com/noisedeck/noisemaker/blob/master/noisemaker/generators.py
            color.g = color.g * -.509 + .276;
            color.b = color.b * -.509 + .198;

            color.rgb = linear_srgb_from_oklab(color.rgb);
            color.rgb = linearToSrgb(color.rgb);
            color.rgb = rgb2hsv(color.rgb);
            color.r = color.r * hueRange * 0.01;
            color.r += 1.0 - (hueRotation / 360.0);
            if (ridges) {
                color.b = 1.0 - abs(color.b * 2.0 - 1.0);
            }
            color.rgb = hsv2rgb(color.rgb);
        }
    } else if (noiseType == 2) {
        // quad tap
        st += vec2(aspectRatio * 0.5, 0.5);
        float speed = loopAmp * 0.02;
        vec3 x0 = vec3(1.0);
        vec3 x1 = vec3(1.0);

        vec3 c1 = rgb2hsv(color1.rgb);
        vec3 c2 = rgb2hsv(color2.rgb);
        vec3 c3 = rgb2hsv(color3.rgb);
        vec3 c4 = rgb2hsv(color4.rgb);

        c1[0] += (sin(time * TAU * speed) + 1.0) * 0.05;
        c2[0] += (sin((0.25 - time) * TAU * speed) + 1.0) * 0.05;
        c3[0] += (sin((0.5 - time) * TAU * speed) + 1.0) * 0.05;
        c4[0] += (sin((0.75 + time) * TAU * speed) + 1.0) * 0.05;

        c1 = hsv2rgb(c1);
        c2 = hsv2rgb(c2);
        c3 = hsv2rgb(c3);
        c4 = hsv2rgb(c4);

        x0.rgb = mix(c1, c2, st.x);
        x1.rgb = mix(c3, c4, st.x);

        color.rgb = mix(x0, x1, 1.0 - st.y);
    }

    color.rgb = brightnessContrast(color.rgb);
    color.a = 1.0;

    st = gl_FragCoord.xy / resolution;

    fragColor = color;
}
